# Infrastructure as Code-workshop med Rett i prod 游

Denne workshopen gir en intro til infrastructure as code (IaC) med [terraform](https://www.terraform.io/). Se slides i [docs](/docs).

NB! Denne workshopen krever at enkelte ressurser er satt opp for 친 bruke egne domenenavn. Dersom du skal g친 gjennom workshopen p친 egenh친nd vil ikke alt fungere.

## F칮r du starter

1. Install칠r `az` og `terraform`, `npm` og `node`, f.eks. ved hjelp av [brew](https://brew.sh/) om du bruker macOS: `brew install azure-cli terraform node@16`. Sjekk at terraform versjonen din er minst `v1.0.0` ved 친 kj칮re `terraform version`, og at node versjonen er minst `v16.0.0` ved 친 kj칮re `node --version`.

1. Det kan v칝re lurt 친 installere en plugin i editoren din. VS Code har f.eks. extensionen "Hashicorp Terraform". Alternativt bruke et JetBrains IDE som IntelliJ med pluginen "HashiCorp Terraform / HCL language support".

1. Du skal ha f친tt en mail fra oss med invitasjon som gir deg tilgang til workshopens Azure tenant. Denne emailen kommer fra `invitations@microsoft.com` og har tittelen "Bekk Terraform Workshop invited you to access applications within their organization". Trykk p친 "Accept Invitation" og f칮lg stegene for 친 f친 tilgang til Azure-portalen (`portal.azure.com`).

1. N친r du har kommet til Azure-portalen, sjekk at det st친r "Bekk Terraform Workshop" 칮verst til h칮yre. Dersom det ikke gj칮r det, trykk p친 profilbildet ditt (칮verst til h칮yre), deretter "Switch directory" og velg "Bekk Terraform Workshop" p친 siden du kommer til.

1. Skriv `az login` i terminalen for 친 logge inn i Azure. N친r det er gjort kan du bruke `az account show` til 친 sjekke at du er logget p친, og at du bruker `iac-workshop`-subscriptionen. Dersom det ikke stemmer kan du bruke `az account set -s iac-workshop` for 친 bytte subscription, verifiser etterp친 med `az account show`.

1. Klon repoet med git

Du er n친 klar til 친 starte!

## Terraform

Dette repoet har tre mapper: `frontend/`, `backend/` og `infrastructure/`. De to f칮rste inneholder koden for hhv. frontenden og backenden, og er med slik at du kan deploye en full app som faktisk fungerer. `infrastructure/` er den mappen som inneholder terraform-koden, og alle kommandoer og nye filer du lager skal ligge i denne mappen, men mindre noe annet er spesifisert.

I `infrastructure/`-mappen er det forel칮pig ikke s친 mye:

* I `terraform.tf` beskrives hvilke *providers* du trenger, og konfigurasjonen av disse. En provider kan sammenliknes med et bibliotek/*library* fra andre programmeringsspr친k. `azurerm` er en slik provider, som definerer ressurser i Azure du kan bruke og oversetter til riktige API-kall n친r du kj칮rer koden.

* `main.tf` inneholder noen konstanter i `locals`-blokken, som kan brukes i programmet. Merk at `locals` kan defineres i en hvilken som helst terraform-fil, og v칝re globalt tilgjengelige i alle andre filer. `main.tf` inneholder ogs친 en definisjon av ressursgruppen som skal opprettes.

* `variables.tf` inneholder variable som gis til terraform. `variable` likner litt p친 `locals`, men disse kan spesifiseres og overskrives n친r terraform kj칮res, f.eks. ved 친 gi et ekstra argument p친 kommandolinjen. Det er litt tungvint 친 spesifisere variable p친 kommandolinjen, s친 vi kommer tilbake til hvordan vi kan gj칮re dette enklere. `location` er den eneste variabelen som er definert forel칮pig, og den har f친tt en default-verdi, s친 det er ikke noe vi trenger 친 gj칮re noe med forel칮pig.

Det var mye tekst, la oss g친 videre til godsakene!

1. F칮r du kan provisjonere infrastruktur med terraform, m친 du initialisere providerne som er spesifisert i `terraform.tf`. Dette kan du gj칮re ved 친 kj칮re `terraform init` (husk 친 kj칮re den fra `infrastructure/`-mappen!). Dette gj칮r ingen endringer p친 infrastrukturen, men oppretter bl.a. `.terraform`-mappen. `terraform init` m친 kj칮res p친 nytt om du 칮nsker 친 installere eller oppgradere providers. **NB!** `.terraform` m친 ikke committes til git, da den kan inneholde sensitiv informasjon.

1. N친r terraform er initialisert kan vi provisjonere infrastruktur ved 친 kj칮re `terraform apply`. F칮rst vil terraform gi en oversikt over endringene som blir gjort. Her opprettes en ressursgruppe i Azure og en random string, `id`, som brukes for 친 automatisk gi unike navn p친 ressursene vi skal opprette, f.eks. ressursgruppen. Skriv `yes` n친r terraform sp칮r om du er sikker p친 om du vil fortsette.

1. Dersom alt gikk fint kan du finne navnet p친 ressursgruppen i en av de siste linjene i outputen:

    ```output
    azurerm_resource_group.rg: Creation complete after 1s [id=/subscriptions/9539bc24-8692-4fe2-871e-3733e84b1b73/resourceGroups/iac-workshop-xxxxxxxx]
    ```

    Det er den siste delen (`iac-workshop-xxxxxxxx`) vi er interessert i. Dette er navnet p친 ressursgruppen, og `xxxxxxxx` vil v칝re den tilfeldige strengen som ble generert.

1. G친 til [Azure-portalen](https://portal.azure.com/), og sjekk at du kan finne ressursgruppen din. Den skal (forel칮pig) v칝re tom.

## Backend

Backend-koden bygget til et Docker-image, som lastes opp i GitHub package registry. Vi skal n친 sette opp en Azure Container Group som laster ned imaget og kj칮rer det som en container.

1. Opprett en ny fil `backend.tf` i `infrastructure/`.
1. Opprett en `locals` blokk i `backend.tf` med f칮lgende innhold:

    ```terraform
    locals {
      server_port = "8080"
      mgmt_port   = "8090"
    }
    ```

   Her opprettes to konstanter `server_port` og `mgmt_port`, som vi kan referere til senere, f.eks. ved 친 skrive `local.server_port`. Verdiene som er gitt er ikke tilfeldige, og samsvarer med det som st친r i `backend/src/main/resources/application.properties`.

1. Vi trenger ogs친 en ny variabel, `backend_image`, i `variables.tf`. Den kan defineres slik:

    ```terraform
    variable "backend_image" {
      type        = string
      description = "The Docker image to run for the backend"
    }
    ```

    Her deklareres en variabel, `backend_image`, og den m친 v칝re av typen `string`. Vi kan referere til denne variablen ved 친 skrive `var.backend_image`.

1. Dersom du n친 kj칮rer `terraform apply` vil du bli bedt om 친 oppgi variabelen. Den vil v칝re `ghcr.io/bekk/iac-workshop-backend:latest`.

1. Vi trenger ikke oppgi variabelen hver gang, for vi kan nemlig putte den i en egen fil som leses automatisk av terraform. Opprett en ny fil `variables.auto.tfvars` med linjen:

    ```terraform
    backend_image = "ghcr.io/bekk/iac-workshop-backend:latest"
    ```

    Dersom du n친 kj칮rer `terraform apply` p친 nytt vil du ikke lenger trenge 친 skrive inn variabelen.

1. Videre skal vi generere en tilfeldig streng som applikasjonen trenger for 친 generere JWT-tokens. Det kan vi gj칮re slik:

    ```terraform
    resource "random_password" "jwt-secret" {
      length  = 64
      special = false
      lower   = true
      upper   = true
      number  = true
    }
    ```

    Denne strengen kan vi senere bruke ved 친 referere til `random_password.jwt-secret.result`.

1. Til sist m친 vi opprette en Azure Container Group som faktisk oppretter backenden:

    ```terraform
    resource "azurerm_container_group" "backend" {
      resource_group_name = azurerm_resource_group.rg.name
      location            = azurerm_resource_group.rg.location
      name                = "${local.resource_prefix}backend"
      ip_address_type     = "Public"
      dns_name_label      = local.unique_id_raw
      os_type             = "Linux"

      container {
        name   = "backend"
        image  = var.backend_image
        cpu    = "1"
        memory = "1"

        ports {
          port     = local.server_port
          protocol = "TCP"
        }

        secure_environment_variables = {
          "JWT_SECRET" = random_password.jwt-secret.result
        }

        readiness_probe {
          http_get {
            path   = "/actuator/health"
            port   = local.mgmt_port
            scheme = "Http"
          }
        }
      }
    }
    ```

    Det var en del kode! La oss brekke det opp litt, og se p친 det viktigste:

    * Vi oppretter her en ressurs av typen `azurerm_container_group`, og kaller denne ressursen for `backend` i f칮rste linje. Merk at `backend` er kun et internt navn i terraform, og ikke navnet ressursen f친r i Azure! Det gj칮r at vi senere kan referere til ressursen med `azurerm_container_group.backend`.

    * Videre gir vi en del argumenter til ressursblokken. F.eks. setter vi `resource_group_name` og `location` til 친 tilsvare ressursgruppen som ble opprettet i `main.tf`. `name` settes ved 친 konkatenere konstanten `local.resource_prefix` (ogs친 fra `main.tf`) med strengen `backend`, slik at ressursnavnet i Azure blir `iac-workshop-backend`.

    * `container`-blokken er veldig lik en container-definisjon i Kubernetes (selv om syntaksen er ulik). Merk spesielt at vi setter `image` til variabelen vi definerte i et tidligere steg. Azure vil laste ned imaget for oss (gitt at imaget er 친pent tilgjengelig) og kj칮re det. `ports`-blokken definerer hvilken port applikasjonen skal v칝re tilgjengelig p친. `readiness_probe`-blokken definerer et endepunkt som kan brukes til 친 sjekke at applikasjonen kj칮rer som den skal, akkurat som i Kubernetes.

1. N친 skal vi ha det som trengs for 친 provisjonere opp backenden med `terraform apply`. Dette vil ta litt tid f칮rste gangen det gj칮res. Sjekk etterp친 at du finner en ressurs av typen "Container instances" i ressursgruppen din som heter `iac-workshop-backend`.

1. Dersom du klikker p친 ressursen vil du f친 en oversikt over noen egenskaper, bl.a. FQDN. Det st친r for "Fully Qualified Domain Name" og er domenenavnet for backenden. Ettersom applikasjonen kj칮rer p친 port `8080` m친 vi legge det p친 i tillegg for 친 koble til. Den fulle addressen blir dermed `xxxxxxx.westeurope.azurecontainer.io:8080`, der `xxxxxxxx` fortsatt er den unike id-en generert av terraform. Denne siden vil gi en feilmelding fordi det ikke er et endepunkt definert av applikasjonen der, men om du g친r til en av applikasjonens definerte endepunkt, f.eks. `xxxxxxxx.westeurope.azurecontainer.io:8080/api/articles` b칮r du f친 en JSON-respons for en artikkel med tittel "Hello World".

1. Det er litt tungvint 친 klikke seg gjennom Azure-portalen for 친 finne domenenavnet til appen, s친 vi kan definere output i terraform i stedet. Opprett en ny fil `outputs.tf`, med f칮lgende kode:

    ```terraform
    output "backend_url" {
      value = "http://${azurerm_container_group.backend.fqdn}:${local.server_port}"
    }
    ```

    Her lager vi en ny `output`, `backend_url`, som best친r av `fqdn` p친 `backend` ressursen, og portnummeret.

1. Kj칮r `terraform apply` p친 nytt, og sjekk at du f친r `backend_url` liknende dette:

    ```output
    Outputs:

    backend_url = "http://xxxxxxxx.westeurope.azurecontainer.io:8080"
    ```

Det var backenden! Dersom du n친 f친r en god respons fra `http://xxxxxxxx.westeurope.azurecontainer.io:8080/api/articles` og `backend_url` som output kan du g친 videre til frontenden.

## Frontend

Vi skal bruke Azure Blob Storage til 친 hoste statiske filer frontend-filer. Forenklet sett skal vi bruke en Azure Storage Account som en tradisjonell webserver.

F칮rst skal vi opprette en ny storage account:

1. Opprett en ny fil, `frontend.tf`, og legg til f칮lgende kode og erstatt `<ressursgruppenavn>` og `<ressursgrupperegion>` med riktige verdier ved 친 bruke ressursgruppe-ressursen som er opprettet tidligere. (Hint: hvordan er dette gjort for andre ressurser vi har allerede har opprettet?)

    ```terraform
    resource "azurerm_storage_account" "web" {
      name                             = local.unique_id_sanitized
      resource_group_name              = <ressursgruppenavn>
      location                         = <ressursgrupperegion>
      account_tier                     = "Standard"
      account_replication_type         = "LRS"
      allow_nested_items_to_be_public  = true
      enable_https_traffic_only        = false
      min_tls_version                  = "TLS1_2"
    }
    ```

    Her skjer det igjen litt forskjellig:

    * Vi har opprettet en ressurs av typen `azurerm_storage_account` som har (det interne) navnet `web`.

    * `name` er satt til `local.unique_id_sanitized`, som er definert i `main.tf`. Navnet p친 en storage account m친 v칝re globalt unikt, dvs. ingen storage accounts i Azure kan ha det samme navnet, dermed m친 vi ha et navn som inneholder en unik id som reduserer sjansen for at noen andre har samme navn.

    * `allow_nested_items_to_be_public` er verdt 친 merke seg. Denne tillater at hvem som helst kan f친 tilgang til blobs (filer) i storage accounten, s친 lenge de kjenner URL-en. Normalt vil det v칝re lurt 친 sette denne `false`, men her 칮nsker vi at andre kan f친 tilgang og setter den til `true`, slik at vi kan bruke den til 친 statiske filer for en allment tilgjengelig nettside.

    * `enable_https_traffic_only` er vanligvis lurt 친 sette til `true`, men forel칮pig skal vi ikke sette opp HTTPS (det kan du gj칮re i ekstraoppgavene p친 slutten).

1. Kj칮r `terraform apply`. N친r storage accounten er opprettet kan du sjekke i ressursgruppen din at du finner en ressurs av typen "Storage account" med navn `iacworkshopxxxxxxxx`.

1. For 친 bruke storage accounten til en server for statiske filer m친 vi skru p친 "static website" featuren. For 친 f친 til det m친 vi legge til en ny `static_website`-blokk inni `azurerm_storage_account`-blokken:

    ```terraform
    resource "azurerm_storage_account" "web" {
      // Samme argumenter som i forrige kodeblokk

      static_website {
        index_document = "index.html"
      }
    }
    ```

    Hva skjer her?

    * Ved 친 legge til `static_website`-blokken vil det opprettes en *storage account container* som vi kan legge filer i. Dette er ikke det samme som en Kubernetes container. En storage account container er en gruppering av *blobs*, og en blob er en fil som kan v칝re p친 et hvilket som helst format, f.eks. tekst eller bilder. Denne storage account containeren vil hete `$web`.

    * `index_document`-argumentet spesifiserer navnet p친 filen som brukes til n친r det kommer en request til rot-URL-en.

1. Kj칮r `terraform apply`. G친 s친 til Azure-portalen, klikk deg inn p친 storage containeren og klikk p친 "Containers" i menyen til venstre.

1. Hva blir s친 URL-en til denne storage accounten? La oss lage en ny `output` i `outputs.tf`:

    ```terraform
    output "storage_account_web_url" {
      value = azurerm_storage_account.web.primary_web_endpoint
    }
    ```

    Her lager vi en ny `output`, som referer til `azurerm_storage_account.web` som vi nettopp opprettet. URL-en er definert i `primary_web_endpoint`.

    Du b칮r da f친 en URL i outputen som likner p친 dette: `https://iacworkshopxxxxxxxx.z6.web.core.windows.net`.

    Dersom du g친r dit vil du f친 feilmeldingen "The requested content does not exist.". Hvorfor det? Vi har jo ikke lastet opp noen filer enda! Mer om det straks!

I dette steget har vi opprettet en ny storage account, med en storage account container `$web`. Dersom du har klart 친 f친 en URL som `storage_account_web_url`-output og f친r en feilmelding n친r du g친r til URL-en i nettleseren er du klar til neste steg.

## Frontend deploy

For at brukere skal kunne se og lage poster i Bekkium m친 frontenden opp. Vi skal her bygge filene manuelt lokalt f칮rst (tilsvarende byggsteget i en CI/CD pipeline), og bruke terraform til 친 laste disse opp i storage accounten.

For 친 kunne n친 de statiske i nettleseren, m친 vi deploye filene i storage account containeren `$web`.

1. For 친 rette frontenden til riktig backend m친 du sette milj칮variabelen `REACT_APP_BACKEND_URL` for bygget. Denne m친 v칝re outputen `backend_url` som du f친r n친r du kj칮rer `terraform apply` (den skal se ut omtrent som `http://xxxxxxxx.westeurope.azurecontainer.io:8080`), pluss `/api` som postfiks. Kommandoen du m친 kj칮re fra `frontend/`-mappen blir dermed omtrent slik:

    ```sh
    # Bash (macOS/Linux/WSL)
    npm ci && REACT_APP_BACKEND_URL="http://xxxxxxxx.westeurope.azurecontainer.io:8080/api" npm build
    # Powershell 7 (Windows)
    $env:REACT_APP_BACKEND_URL="http://xxxxxxxx.westeurope.azurecontainer.io:8080/api"
    npm ci
    npm build
    ```

    Dersom alt gikk bra ligger n친 den ferdigbygde frontenden i `frontend/build/`-mappen, klar for bruk i senere steg.

1. For 친 hjelpe deg litt p친 vei har vi definert noen lokale variable some blir nyttige, disse kan puttes p친 toppen av `frontend.tf`.

    ```terraform
    locals {
      frontend_dir   = "${path.module}/../frontend"
      frontend_files = fileset(local.frontend_dir, "**")
      frontend_src = {
        for fn in local.frontend_files :
        fn => filemd5("${local.frontend_dir}/${fn}") if(length(regexall("(node_modules/.*)|build/.*", fn)) == 0)
      }

      mime_types = {
        ".gif"  = "image/gif"
        ".html" = "text/html"
        ".ico"  = "image/vnd.microsoft.icon"
        ".jpeg" = "image/jpeg"
        ".jpg"  = "image/jpeg"
        ".js"   = "text/javascript"
        ".json" = "application/json"
        ".map"  = "application/json"
        ".png"  = "image/png"
        ".txt"  = "text/plain"
      }
    }
    ```

1. N친r frontend-filene er bygget i `frontend/build` er de klare for opplastning. Vi skal n친 laste opp hver enkelt fil som en blob til `$web` containeren. Dette er prima use case for [l칮kker](https://www.terraform.io/docs/language/meta-arguments/for_each.html). For at filene skal tolkes riktig m친 MIME-typen v칝re rett. Vi har allerede definert de n칮dvendige typene i `local.mime_types` map-et. Bruk [`lookup`-funksjonen](https://www.terraform.io/docs/language/functions/lookup.html) for 친 setter `content_type` til riktig MIME-type. (Hint: `regex("\\.[^.]+$", basename(each.value))` gir deg filendingen, og default kan v칝re `null`).

    ```terraform
    resource "azurerm_storage_blob" "payload" {
      // Vi trenger kun ferdige statiske filer
      for_each               = fileset("${local.frontend_dir}/build", "**")
      name                   = each.value
      storage_account_name   = azurerm_storage_account.web.name
      storage_container_name = "$web"
      type                   = "Block"
      source                 = "${local.frontend_dir}/build/${each.value}"
      content_md5            = filemd5("${local.frontend_dir}/build/${each.value}")
      content_type           = <sett inn riktig MIME-type>
    }
    ```

1. Kj칮r `terraform apply`. Dersom alt g친r fint, skal du n친 se en nettside dersom du navigerer til URL-en for storage accounten (`storage_account_web_url` output-variabelen).

Dersom nettsiden fungerer er du ferdig med dette steget.

Terraform er ikke n칮dvendigvis den beste m친ten 친 deploye kode p친, men vi har tatt det med her for 친 vise at det er _mulig_. I et reelt scenario ville man kanskje 칮nske 친 gj칮re det p친 andre m친ter.

## DNS

Til slutt skal vi sette opp et eget domene for appen. Denne gangen har vi satt opp domenet `rettiprod.live` og appen din skal f친 et subdomene p친 `xxxxxxxx.rettiprod.live`.

1. Opprett filen `dns.tf`. Og legg til f칮lgende kode:

    ```terraform
    locals {
      assumed_storage_account_web_host = "${local.unique_id_sanitized}.z6.web.core.windows.net"
    }
    ```

1. Videre har vi lagd satt opp de n칮dvendige, delte ressursene for domenet `rettiprod.live` i ressursgruppen `workshop-admin`. Vi m친 referere til disse ressursene for 친 lage et subdomene. Det gj칮r vi ved 친 opprette f칮lgende `data`-blokk:

    ```terraform
    data "azurerm_dns_zone" "rettiprod_live" {
      name                = "rettiprod.live"
      resource_group_name = "workshop-admin"
    }
    ```

1. Til slutt m친 vi lage subdomenet. Det gj칮r vi ved 친 opprette en CNAME record, som peker fra navnet p친 subdomenet til URL-en til storage accounten. Det kan vi gj칮re slik:

    ```terraform
    resource "azurerm_dns_cname_record" "www" {
      zone_name           = data.azurerm_dns_zone.rettiprod_live.name
      resource_group_name = data.azurerm_dns_zone.rettiprod_live.resource_group_name

      ttl    = 60
      name   = local.unique_id_raw
      record = local.assumed_storage_account_web_host
    }
    ```

    * Legg merke til at `resource_group_name` her blir `workshop-admin`, og ikke ressursgruppen du tidligere har opprettet. Dette er fordi alle DNS-ressursene m친 ligge i samme ressursgruppe.

    * `name` her blir navnet p친 subdomenet, i v친rt tilfelle den unike ID-en `xxxxxxxx` som terraform har generert for deg, og `record` er URL-en til den statiske nettsiden i storage accounten.

1. Kj칮r `terraform apply`. Du kan sjekke at dette ble opprettet riktig ved 친 g친 til `rett-i-prod-admin` ressursgruppen i Azure-portalen. Trykke p친 ressursen som heter `rettiprod.live` og sjekke at det er opprettet en CNAME record, med samme navn som din unike id (`xxxxxxxx`).

1. N친 m친 vi oppdatere `azurerm_storage_account` ressursen i `frontend.tf` slik at den aksepterer requests med det nye domenenavnet. Storage accounten m친 n친 provisjoneres opp etter at DNS-recorden er klar, hvis ikke vil det ikke fungere. Det kan vi ordne ved 친 legge in et [`depends_on`-array](https://www.terraform.io/docs/language/meta-arguments/depends_on.html).

    ```terraform
    resource "azurerm_storage_account" "web" {
        // Argumentene fra tidligere er uforandret

      custom_domain {
        name          = local.web_hostname
        use_subdomain = false
      }

      // Legg til depends_on her.
    }
    ```

1. Lag en ny output-variabel, `frontend_url` som gir oss den nye URL-en til frontenden.

1. Kj칮r `terraform apply` og g친 til URL-en du f친r i output.

Dersom du f친r den nye nye URL-en som output (den skal se ca. slik ut: `http://xxxxxxx.rettiprod.live`) og den fungerer, er du ferdig. Bra jobba! 游녪

## Ekstra

Du har n친 f친tt hobbyprosjeketet ditt ut i prod! 游 Hvis du har tid til overs s친 har vi noen ekstraoppgaver du kan pr칮ve deg p친. Du kan selv velge hvilke du vil gj칮re, de fleste er ikke i en spesiell rekkef칮lge.

### Slett ressursene du har opprettet

Dersom du 칮nsker 친 slette alle ressursene kan du kj칮re `terraform destroy`. Dette vil fjerne alle ressursene i Azure, og nullstille terraform-tilstanden. Dersom du 칮nsker 친 opprette ressursene p친 nytt kan du kj칮re `terraform apply` igjen, og alle ressursene vil opprettes p친 nytt.

Merk at ettersom all tilstanden slettes av `terraform destroy`, vil den unike id-en bli generert p친 nytt av terraform. Dermed blir ogs친 ressursgruppenavnet og URL-ene nye.

**NB!** `terraform destroy` vil ugjenopprettelig slette data som ikke er definert av terraform. F.eks. data i databaser, hemmeligheter i key vaults eller brukeropplastede filer i en storage account. I denne workshopen er det trygt, men v칝r forsiktig om du bruker terraform til faktiske applikasjoner.

### Les om terraform-provideren for Azure

Her kan du sl친 opp de ulike ressursene vi har brukt, og pr칮ve 친 finne forklaringen p친 ressursblokker eller argumenter du ikke forst친r. Dokumentasjonen finner du [her](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs).

### Sett opp en database

Backenden st칮tter f칮lgende databaser: H2, MSSQL, MySQL og PostgreSQL. Som standard [benyttes H2](./backend/src/main/resources/application.properties) (in-memory database). Finn ut hvordan man konfigurerer en alternativ database via milj칮variabler, samt hvordan man provisjonerer en med Terraform (f.eks. [`azurerm_postgresql_server`](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/postgresql_server).

### Sl친 p친 HTTPS for backend

Det kan v칝re nyttig med HTTPS. Det enkleste er en l칮sning som h친ndterer HTTPS sertifikater automatisk for oss, f.eks. ved 친 spinne opp en ny container i Azure Container Instances som fungerer som en *reverse proxy* og tar seg av dette.

Caddy kan brukes som reverse proxy. Container-imaget `caddy` inneholder alt du trenger, og kj칮res ved 친 bruke kommandoen `caddy reverse-proxy --from <ekstern-aci-url> --to <intern-backend-url>` n친r containeren skal startes. Du vil ogs친 trenge 친 konfigurere et `volume` for containeren, der Caddy-instansen kan lagre data. Dette gj칮res enklest ved 친 lage en file share i en storage account (`azurerm_storage_share`). Konfigurer port `80` og `433` for containeren.

Oppdat칠r `backend_url` outputen til 친 bruke `https` og fjern portspesifikasjonen (den vil da automatisk bruke `443`).

Test at det fungerer ved 친 sjekke at du f친r suksessfull respons fra `https://xxxxxxxx.rettiprod.live/api/articles`.

Videre b칮r man bygge frontenden p친 nytt (etterfulgt av en ny `terraform apply`), med ny `REACT_APP_BACKEND_URL` til 친 bruke HTTPS fremfor HTTP for 친 unng친 advarsler om og problemer med [mixed content](https://developer.mozilla.org/en-US/docs/Web/Security/Mixed_content). Kommandoen for 친 bygge frontenden b칮r n친 se omtrent slik ut:

```sh
npm ci && REACT_APP_BACKEND_URL="https://xxxxxxxx.rettiprod.live/api" npm build
```

Nyttige lenker:

* [Azure Container Instance](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/container_group)
* [Storage Account](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/storage_account) og [file share](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/storage_share)

### Sl친 p친 HTTPS for frontend med eget domene

For 친 gj칮re dette steget m친 HTTPS fungere for backend f칮rst. Storage accounten st칮tter HTTPS ut av boksen med sitt eget domene (typisk `<storage-account-navn>.z6.web.core.windows.net`), men om vi skal ha HTTPS for eget domene blir det komplisert. Det finnes flere m친ter 친 gj칮re dette p친, men her skal vi sette opp en CDN som h친ndterer sertifikatet for oss. Terraform-dokumentasjonen for [`azurerm_cdn_endpoint_custom_domain`](https://registry.terraform.io/providers/hashicorp/azurerm/latest/docs/resources/cdn_endpoint_custom_domain) har et godt eksempel p친 hvordan en CDN kan settes opp med eget domene. I tillegg m친 du bruke underressursen `cdn_managed_https` for 친 f친 HTTPS for domenet. Merk at n친r du skrur p친 HTTPS vil `terraform apply` kj칮re til sertifikater er ferdig provisjonert av Azure - dette kan ta opp mot en time, i denne workshopen anbefaler vi at du gj칮r dette sist.

Du kan n친 ogs친 sette `enable_https_traffic_only` til `true` for storage accounten.

### Gj칮re endringer p친 applikasjonene

1. Lag en fork av dette repoet (bruk knappen 칮verst til h칮yre), og lag en fork som ligger under din egen bruker. URL-en til det nye repoet blir da `https://github.com/<ditt-github-brukernavn>/iac-workshop`.

1. G친 til din fork av dette repoet. Her m친 du gj칮re noen instillinger:
   1. G친 til `Actions` i menyen. Her skal du skru p친 GitHub Actions for ditt repo, slik at du f친r automatiserte bygg av frontend og backend. Byggene (GitHub kaller dette "workflows") vil bare kj칮re dersom det er gjort endringer i hhv. `frontend/` eller `backend/` mappene i repoet.
   1. N친r automatiserte bygg er skrudd p친, m친 vi kj칮re dem manuelt f칮rste gang. For hvert av byggene, trykk p친 "Run workflow" for 친 kj칮re koden. Du trenger ikke laste ned artifaktene som lages av bygget, det gj칮res automatisk n친r koden kj칮res.
   1. N친r frontend-bygget er ferdig, kan du se at artifakten er lastet opp p친 `https://github.com/<ditt-github-brukernavn>/iac-workshop/releases`.
   1. Backend-bygget legges i ditt private image-registry. Det bygde Docker-imaget kan du finne p친 `https://github.com/<ditt-github-brukernavn>?tab=packages>`.
